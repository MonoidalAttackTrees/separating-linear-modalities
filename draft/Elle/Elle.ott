% Input variables.
metavar vars ,  n , a, x , y, z , w , m , o ::=  
indexvar ivar, i , k , j , l ::=
metavar const, b ::= 

grammar


% Lambek category:
A, B, C :: 'lform_' ::=
  | Base    ::   :: Base          
  {{tex \mathsf{B} }}
  | Unit    ::   :: Unit          
  | A (x) B ::   :: Tensor        
  | A -> B  ::   :: RFun          
  | A <- B  ::   :: LFun          
  | ( A )   :: M :: Parens 
  | h( A )  :: M :: HiddenParens 
  {{ tex [[A]] }}
  | F X     ::   :: LeftAdj
  {{tex \mathsf{F}[[X]] }}

% Symmetric monoidal closed category:
X, Y, Z :: 'sform_' ::=
  | Base    ::   :: Base          
  {{tex \mathsf{B} }}
  | Unit    ::   :: Unit          
  | X (x) Y ::   :: Tensor        
  | X -> Y  ::   :: LFun          
  | X <- Y  ::   :: RFun          
  | ( X )   :: M :: Parens 
  | h( X )  :: M :: HiddenParens 
  {{ tex [[X]] }}
  | Gf A     ::   :: RightAdj
  {{tex \mathsf{G}[[A]] }}

T :: 'type_' ::=
  | A :: :: LType
  | X :: :: SType

p :: 'pattern_' ::=
  | - :: :: hole
  {{tex \star }}
  | x :: :: var
  | triv :: :: triv
  {{tex \mathsf{triv} }}
  | p (x) p' :: :: ten
  | F x :: :: LeftAdj
  {{tex \mathsf{F} [[x]] }}
  | Gf x     ::   :: RightAdj
  {{tex \mathsf{G}[[x]] }}

t :: 'term_' ::=
  | x :: :: Var
  | b :: :: Const
  | triv :: :: Unit
  {{tex \mathsf{triv} }}
  | let t1 : T be p in t2 :: :: let
  {{tex \mathsf{let}\,[[t1]] : [[T]]\,\mathsf{be}\,[[p]]\,\mathsf{in}\,[[t2]] }}
  | t1 (x) t2 :: :: Tensor
  | \l x : A . t :: :: LFun
  {{tex \lambda_l [[x]] : [[A]].[[t]] }}
  | \r x : A . t :: :: RFun
  {{tex \lambda_r [[x]] : [[A]].[[t]] }}
  | \ x : A . t :: :: Fun
  {{tex \lambda [[x]] : [[A]].[[t]] }}  
  | appl t1 t2   :: :: Appl
  {{tex \mathsf{app}_l\,[[t1]]\,[[t2]] }}
  | appr t1 t2   :: :: Appr
  {{tex \mathsf{app}_r\,[[t1]]\,[[t2]] }}
  | app t1 t2   :: :: App
  {{tex \mathsf{app}\,[[t1]]\,[[t2]] }}  
  | ex x1 , x2 with t1 , t2 in t3 :: :: Ex
  {{tex \mathsf{ex}\,[[x1]],[[x2]]\,\mathsf{with}\,[[t1]],[[t2]]\,\mathsf{in}\,[[t3]] }}
  | contrR x1 as t1 , t2 in t3 :: :: ContrR
  {{tex \mathsf{contrR}\,[[x1]]\,\mathsf{as}\,[[t1]],[[t2]]\,\mathsf{in}\,[[t3]] }}
  | contrL x1 as t1 , t2 in t3 :: :: ContrL
  {{tex \mathsf{contrL}\,[[x1]]\,\mathsf{as}\,[[t1]],[[t2]]\,\mathsf{in}\,[[t3]] }}
  | weak x in t :: :: Weak
  {{tex \mathsf{weak}\,[[x]]\,\mathsf{in}\,[[t]] }}
  | [ t1 / x ] t2 :: M :: Subst
  | [ s / x ] t   :: M :: Subst2
  | ( t )         :: S :: parens
  | h( t )        :: M :: hparen
  {{tex [[t]] }}
  | F s           ::   :: LeftAdj
  {{tex \mathsf{F}[[s]] }}

s :: 'sterm_' ::=
  | x :: :: Var
  | b :: :: Const
  | triv :: :: Unit
  {{tex \mathsf{triv} }}
  | let s1 : X be p in s2 :: :: let
  {{tex \mathsf{let}\,[[s1]] : [[X]]\,\mathsf{be}\,[[p]]\,\mathsf{in}\,[[s2]] }}
  | s1 (x) s2 :: :: Tensor
  | \l x : X . s :: :: LFun
  {{tex \lambda_l [[x]] : [[X]].[[s]] }}
  | \r x : X . s :: :: RFun
  {{tex \lambda_r [[x]] : [[X]].[[s]] }}
  | \ x : X . s :: :: Fun
  {{tex \lambda [[x]] : [[X]].[[s]] }}  
  | appl s1 s2   :: :: Appl
  {{tex \mathsf{app}_l\,[[s1]]\,[[s2]] }}
  | appr s1 s2   :: :: Appr
  {{tex \mathsf{app}_r\,[[s1]]\,[[s2]] }}
  | app s1 s2   :: :: App
  {{tex \mathsf{app}\,[[s1]]\,[[s2]] }}  
  | ex x1 , x2 with s1 , s2 in s3 :: :: Ex
  {{tex \mathsf{ex}\,[[x1]],[[x2]]\,\mathsf{with}\,[[s1]],[[s2]]\,\mathsf{in}\,[[s3]] }}
  | contrR x1 as s1 , s2 in s3 :: :: ContrR
  {{tex \mathsf{contrR}\,[[x1]]\,\mathsf{as}\,[[s1]],[[s2]]\,\mathsf{in}\,[[s3]] }}
  | contrL x1 as s1 , s2 in s3 :: :: ContrL
  {{tex \mathsf{contrL}\,[[x1]]\,\mathsf{as}\,[[s1]],[[s2]]\,\mathsf{in}\,[[s3]] }}
  | weak x in s :: :: Weak
  {{tex \mathsf{weak}\,[[x]]\,\mathsf{in}\,[[s]] }}  
  | [ s1 / x ] s2 :: M :: Subst
  | ( s )         :: S :: parens
  | h( s )        :: M :: hparen
  | Gf t           ::   :: RightAdj
  {{tex \mathsf{G}[[t]] }}

G {{tex \Gamma}}, D {{tex \Delta}}, I {{tex \Phi}}, P {{tex \Psi}} :: 'Ctx_' ::=
  | .       :: :: Empty
  {{tex \cdot }}
  | G1 , G2 :: :: Ext
  | x : A   :: :: El
  | ( G )   :: S :: Paren
  | x : X   ::   :: EParen

formula :: 'formula_' ::=
  | judgement            ::   :: judgement
  | formula1 && formula2 :: M :: quad
  {{tex [[formula1]] \quad [[formula2]] }}
  | formula1 ... formulai :: M :: vec
  | ( formula )          :: S :: parens
  {{tex [[formula]] }}
  | x nin FV ( t ) :: :: FV
  {{tex [[x]] \not\in \mathsf{FV}([[t]]) }}
  | x nin1 | G , D , P | :: :: NiNCtx1
  {{tex [[x]] \not\in | [[G]], [[D]], [[P]] | }}
  | x nin2 | G , D |     :: :: NiNCtx2
  {{tex [[x]] \not\in | [[G]], [[D]] | }}

terminals :: 'terminals_' ::=
  | Unit :: :: Unit
  {{tex \mathsf{Unit} }}
  | (x)  :: :: Tensor
  {{tex \otimes }}
  | (e)  :: :: ETensor
  {{tex \circop{e} }}  
  | (w)  :: :: WTensor
  {{tex \circop{w} }}  
  | (c)  :: :: CTensor
  {{tex \circop{c} }}
  | ->   :: :: LFun
  {{tex \rightharpoonup }}
  | <-   :: :: RFun
  {{tex \leftharpoonup }}
  | -o   :: :: Limp
  {{tex \multimap }}
  | |-   :: :: Vdash
  {{tex \vdash }}
 
defns
  Jtype :: '' ::=

defn
  G |- s : X :: :: sty :: 'S_'
by 

  -------------- :: var
  x : X |- x : X

  G,D |- s : X
  --------------------------------------------- :: unitL
  G,x : Unit,D |- let x : Unit be triv in s : X

  ---------------- :: unitR
  . |- triv : Unit

  G, x : X, y : Y,D |- s : Z
  --------------------------------------------- :: beta
  G, z : Y, w : X,D |- ex w,z with x,y in s : Z

  G1, x : X, G2, y : X, G3 |- s : Y
  --------------------------------------------- :: contrR
  G1, G2, z : X, G3 |- contrR z as x,y in s : Y

  G1, x : X, G2, y : X, G3 |- s : Y
  --------------------------------------------- :: contrL
  G1, z : X, G2, G3 |- contrL z as x,y in s : Y

  G, D |- s : Y && x nin2 |G,D|
  ------------------------------ :: weak
  G, x : X, D |- weak x in s : Y

  G |- s1 : X && D1,x : X,D2 |- s2 : Y
  ------------------------------------ :: cut
  D1,G,D2 |- [s1/x]s2 : Y

  G,x : X,y : Y,D |- s : Z
  ------------------------------------------------------ :: tenL
  G,z : X (x) Y,D |- let z : X (x) Y be x (x) y in s : Z

  G |- s1 : X && D |- s2 : Y
  -------------------------- :: ten
  G,D |- s1 (x) s2 : X (x) Y  

  G |- s1 : X && D1,x : Y,D2 |- s2 : Z
  ----------------------------------------- :: impLl
  D1,G,y : X -> Y,D2 |- [appl y s1/x]s2 : Z

  G |- s1 : X && D1,x : Y,D2 |- s2 : Z
  ----------------------------------------- :: impL2
  D1,y : Y <- X,G,D2 |- [appr y s1/x]s2 : Z

  G, x : X |- s : Y
  ---------------------- :: impRl
  G |- \l x:X.s : X -> Y

  x : X,G |- s : Y
  ---------------------- :: impRr
  G |- \r x:X.s : Y <- X

  G ; . |- t : A
  ---------------- :: Gr
  G |- Gf t : Gf A

defn
  G ; P |- t : A :: :: lty :: 'L_'
by

  ------------------ :: ax
  . ; x : A |- x : A

  G,D;P |- t : A
  ----------------------------------------------- :: unitl1
  G,x : Unit,D;P |- let x : Unit be triv in t : A

  G;P,I |- t : A
  ----------------------------------------------- :: unitl2
  G;P,x : Unit,I |- let x : Unit be triv in t : A

  -------------------- :: unitr
  . ; . |- triv : Unit

  G, x : X, y : Y,D;P |- t : A
  ----------------------------------------------- :: beta
  G, z : Y, w : X,D;P |- ex w,z with x,y in t : A

  G, D ; P |- t : B && x nin1 |G,D,P|
  ----------------------------------- :: weak
  G, x : X, D ; P |- weak x in t : B

  G1, x : X, G2, y : X, G3 ; P |- t : B
  ------------------------------------------------- :: contrR
  G1, G2, z : X, G3 ; P |- contrR z as x,y in t : B

  G1, x : X, G2, y : X, G3 ; P |- t : B
  ------------------------------------------------- :: contrL
  G1, z : X, G2, G3 ; P |- contrL z as x,y in t : B

  G |- s : X && D1,x : X,D2;P |- t : A
  ------------------------------------ :: cut1
  D1,G,D2;I |- [s/x]t : A

  G;P |- t1 : A && D;I1,x : A,I2 |- t2 : B
  ---------------------------------------- :: cut2
  G,D;I1,P,I2 |- [t1/x]t2 : B

  G,x : X,y : Y,D;P |- t : A
  -------------------------------------------------------- :: tenL1
  G,z : X (x) Y,D;P |- let z : X (x) Y be x (x) y in t : A

  G;P,x : A, y : B,I |- t : A
  -------------------------------------------------------- :: tenL2
  G;P,z : A (x) B,I |- let z : A (x) B be x (x) y in t : A

  G;P |- t1 : A && D;I |- t2 : B
  ------------------------------ :: tenR
  G,D;P,I |- t1 (x) t2 : A (x) B

  G |- s : X && D1,x : Y,D2;P |- t : A
  ----------------------------------------- :: impLl
  D1,G,y : X -> Y,D2;P |- [appl y s/x]t : A

  G |- s : X && D1,x : Y,D2;P |- t : A
  ------------------------------------------ :: impL2
  D1,y : Y <- X,G,D2;P |- [appr y s1/x]t : A

  G;P |- t1 : A && D;I1,x : B,I2 |- t2 : A
  --------------------------------------------- :: impL3
  G,D;I1,P,y : A -> B,I2 |- [appl y t1/x]t2 : A

  G;P |- t1 : A && D;I1,x : B,I2 |- t2 : A
  --------------------------------------------- :: impL4
  G,D;I1,y : B <- A,P,I2 |- [appl y t1/x]t2 : A

  G;P,x : A |- t : B
  ------------------------ :: impRl
  G;P |- \l x:A.t : A -> B

  G;x : A,P |- t : B
  ------------------------ :: impRr
  G;P |- \r x:A.t : B <- A

  G |- s : X
  ---------------- :: Fr
  G;. |- F s : F X

  G,x : X;P |- t : A
  ------------------------------------------ :: Fl
  G;z : F X,P |- let z : F X be F x in t : A

  G;P,x : A |- t : B
  --------------------------------------------- :: Gl
  z : Gf A,G;P |- let z : Gf A be Gf x in t : B
